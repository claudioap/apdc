/* BUILT IN DEFINITIONS */
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

/* ########## DATA TYPES ########## */
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
boolean = { "true" | "false" }
inner = @{ char* }
string = ${ "\"" ~ inner ~ "\"" }

pair = { identifier ~ ":" ~ evaluable }
dict = {
  "{"
  	~ pair // At least one pair required
    ~ ("," ~ pair)* // Pairs separated with commas
  ~"}"
}

/* ########## VARIABLES AND EVALUATION ########## */
identifier = @{!ASCII_DIGIT ~(ASCII_ALPHANUMERIC | "_" | "-")+}
value = _{string | number | boolean | dict | identifier }
evaluable = _{ "("~ evaluable ~")" | function_call | object | object_call | value | identifier}


binop = _{add|sub|mul|div|rem|pow|eq|le|leq|gr|geq|and|or}
add =  {"+"}
sub =  {"-"}
mul =  {"*"}
div =  {"/"}
rem =  {"%"}
pow =  {"^"}
neg = {"!"}
eq =  {"=="}
neq =  {"!="}
le =  {"<"}
leq =  {"<="}
gr =  {">"}
geq =  {">="}
and =  {"&&"}
or =  {"||"}


expression = {term ~ (binop ~ term)*}
term = _{ evaluable | "(" ~ expression ~ ")" }


/* ########## COMPOSITE TYPES ########## */
parameter = { string | identifier }
parameters = {"(" ~  identifier ~ ("," ~ identifier)*~ ")"}
arguments = {"(" ~ (expression ~ ("," ~ expression)*)?~ ")"}
function = {
    "Fun" ~ parameters? // arguments (including brackets) optional
    ~ "{"  ~ statement* ~ "}"
}

/* ########## FLOW CONTROL ########## */
body = { statement+ }
conditional_if = {
    "if" ~ expression ~ "{"
    ~ body ~ "}" ~
    (conditional_elif)* ~
    (conditional_else)?
}

conditional_elif = {
    "elif" ~ expression ~ "{"
        ~ body
    ~"}"
}

conditional_else = {
    "else" ~ "{"
        ~ body
    ~"}"
}

cycle = _{cycle_loop|cycle_while|cycle_do_while|cycle_for}
cycle_loop = {
    "loop" ~ "{"
        ~ body
    ~"}"
}
cycle_while = {
     "while" ~ expression ~ "{"
         ~ body
     ~"}"
}
cycle_do_while = {
    "do" ~ "{"
        ~ body
    ~"}" ~ "while" ~ expression
}
cycle_for = {
    "for" ~ assignment ~ ";" ~ expression ~ ";" ~ statement ~ "{"
        ~ body
    ~"}"
}

/* ########## OPERATIONS ########## */
assignment = {identifier ~ "=" ~ (function|protocol|expression)}
function_call = {identifier ~ arguments}
object_call = {identifier ~ "." ~ identifier ~ arguments}

statement = {assignment|conditional_if|cycle|function_call|object_call}
program = _{ SOI ~ statement+ ~ EOI }


/* ########## YGGDRASIL ########## */
protocol = {
    "Proto" ~ dict
}
object_type = {("Protocol"|"Response"|"Request"|"Timer")}
object =  {object_type ~ dict}
